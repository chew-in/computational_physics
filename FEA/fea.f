      PROGRAM FEA 

C *** INITIALIZE. GIVE YOURSELF PLENTY ROOM ON THE TIME DIMENSION

      REAL, DIMENSION (9,9,50000) :: T_GEL = 0.0
      INTEGER STEP,TOTAL_STEPS
      
C *** THERMAL DIFFUSION COEFFICIENT IN CM^2/S      
      D=0.002
      
      OPEN(UNIT=1,FILE='fea.out',STATUS='UNKNOWN')

C *** HOW FAST DO WE RAMP TEMPERATURE? (0-15 C)
      WRITE(*,*)'ENTER NUMBER OF HOURS FOR THE RAMP'
      READ(*,*)HOURS
      T_SLOPE=15./(HOURS*3600.)
      
C *** CHOOSE TIME STEP IN S. SHOULD BE < 125 S FOR CONVERGENCE 
C *** WITH PRESENT THERMAL DIFFUSION COEFFICIENT. DETERMINE # OF 
C *** TIME STEPS
      
      TIME_STEP=100.
      TOTAL_STEPS=INT(HOURS*3600./TIME_STEP)
      
      
C *** BUILD THE MATRIX PROGRESSIVELY
C *** STEP = 1 IS THE INITIAL CONDITION (ALL T=0 C)
      
      DO 7 STEP = 2 , TOTAL_STEPS
         
C *** IMPOSE BOUNDARY CONDITIONS
         
      T_GEL(1,:,STEP) = (STEP-1) * TIME_STEP * T_SLOPE   
      T_GEL(9,:,STEP) = (STEP-1) * TIME_STEP * T_SLOPE   
      T_GEL(:,1,STEP) = (STEP-1) * TIME_STEP * T_SLOPE   
      T_GEL(:,9,STEP) = (STEP-1) * TIME_STEP * T_SLOPE   

C *** APPLY EXPLICIT FINITE-DIFFERENCE EQUATION TO ALL OTHER CELLS
C *** DELTA X AND DELTA Y ARE 1 CM
      
      DO 14 I=2,8  
      DO 21 J=2,8  
         
      GROUP = T_GEL(I-1,J,STEP-1) + T_GEL(I+1,J,STEP-1) +
     M T_GEL(I,J-1,STEP-1) + T_GEL(I,J+1,STEP-1) 
     M -4.*T_GEL(I,J,STEP-1)

      T_GEL(I,J,STEP) = D*TIME_STEP*GROUP + T_GEL(I,J,STEP-1) 
      
   21 CONTINUE      
   14 CONTINUE   
      
C *** WRITE TO FILE THE MAXIMUM GRADIENT OF T AT THIS POINT IN TIME
      
      X=(STEP-1)*TIME_STEP
      WRITE(1,*)X, MAXVAL(T_GEL(:,:,STEP)) - MINVAL(T_GEL(:,:,STEP))
      
    7 CONTINUE   

      STOP
      END
      

